defmodule SymphonyElixirWeb.ObservabilityApiController do
  @moduledoc """
  JSON API for Symphony observability data.
  """

  use Phoenix.Controller, formats: [:json]

  alias Plug.Conn
  alias SymphonyElixir.AgentConsole
  alias SymphonyElixirWeb.{Endpoint, ObservabilityPubSub, Presenter}

  @spec state(Conn.t(), map()) :: Conn.t()
  def state(conn, _params) do
    json(conn, Presenter.state_payload(orchestrator(), snapshot_timeout_ms()))
  end

  @spec issue(Conn.t(), map()) :: Conn.t()
  def issue(conn, %{"issue_identifier" => issue_identifier}) do
    case Presenter.issue_payload(issue_identifier, orchestrator(), snapshot_timeout_ms()) do
      {:ok, payload} ->
        json(conn, payload)

      {:error, :issue_not_found} ->
        error_response(conn, 404, "issue_not_found", "Issue not found")
    end
  end

  @spec console(Conn.t(), map()) :: Conn.t()
  def console(conn, %{"issue_identifier" => issue_identifier}) do
    with {:ok, payload, workspace_path} <- console_context(issue_identifier),
         {:ok, transcript} <- AgentConsole.read_transcript(workspace_path) do
      json(conn, %{issue_identifier: issue_identifier, console: payload.console, transcript: transcript})
    else
      {:error, :issue_not_found} ->
        error_response(conn, 404, "issue_not_found", "Issue not found")

      {:error, :console_unavailable} ->
        error_response(conn, 404, "console_unavailable", "Console not available")

      {:error, reason} ->
        error_response(conn, 500, "console_read_failed", "Console read failed: #{inspect(reason)}")
    end
  end

  @spec console_command(Conn.t(), map()) :: Conn.t()
  def console_command(conn, %{"issue_identifier" => issue_identifier, "command" => command}) do
    with {:ok, _payload, workspace_path} <- console_context(issue_identifier),
         {:ok, result} <- AgentConsole.submit_command(workspace_path, command) do
      ObservabilityPubSub.broadcast_update()
      json(conn, result)
    else
      {:error, :issue_not_found} ->
        error_response(conn, 404, "issue_not_found", "Issue not found")

      {:error, :console_unavailable} ->
        error_response(conn, 404, "console_unavailable", "Console not available")

      {:error, {:unsupported_command, message}} ->
        conn
        |> put_status(422)
        |> json(%{error: %{code: "unsupported_console_command", message: message}})
    end
  end

  @spec refresh(Conn.t(), map()) :: Conn.t()
  def refresh(conn, _params) do
    case Presenter.refresh_payload(orchestrator()) do
      {:ok, payload} ->
        conn
        |> put_status(202)
        |> json(payload)

      {:error, :unavailable} ->
        error_response(conn, 503, "orchestrator_unavailable", "Orchestrator is unavailable")
    end
  end

  @spec method_not_allowed(Conn.t(), map()) :: Conn.t()
  def method_not_allowed(conn, _params) do
    error_response(conn, 405, "method_not_allowed", "Method not allowed")
  end

  @spec not_found(Conn.t(), map()) :: Conn.t()
  def not_found(conn, _params) do
    error_response(conn, 404, "not_found", "Route not found")
  end

  defp error_response(conn, status, code, message) do
    conn
    |> put_status(status)
    |> json(%{error: %{code: code, message: message}})
  end

  defp orchestrator do
    Endpoint.config(:orchestrator) || SymphonyElixir.Orchestrator
  end

  defp snapshot_timeout_ms do
    Endpoint.config(:snapshot_timeout_ms) || 15_000
  end

  defp console_context(issue_identifier) when is_binary(issue_identifier) do
    case Presenter.issue_payload(issue_identifier, orchestrator(), snapshot_timeout_ms()) do
      {:ok, %{workspace: %{path: workspace_path}, console: console} = payload}
      when is_binary(workspace_path) and is_map(console) ->
        {:ok, payload, workspace_path}

      {:ok, _payload} ->
        {:error, :console_unavailable}

      {:error, :issue_not_found} ->
        {:error, :issue_not_found}
    end
  end
end
